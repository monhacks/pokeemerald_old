raw `
Alnerwick_Library_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, AlnerwickLibrary_OnTransition
	.byte 0

AlnerwickLibrary_OnTransition:
    call_if_set FLAG_ALNERWICKLIBRARY_THIEF, AlnerwickLibrary_Mapscript_Librarian
    end
`
script AlnerwickLibrary_Object_Boy {
    msgbox(format("{COLOR BLUE}The {COLOR GREEN}Alnerwick Library{COLOR BLUE} is full of lots of information!\pSome of the books here go back over 200 years!"), MSGBOX_NPC)
    end
}
script AlnerwickLibrary_Object_Girl {
    lock
    msgbox(format("{COLOR RED}Hmmm..."), MSGBOX_AUTOCLOSE)
    faceplayer
    delay(5)
    msgbox(format("{COLOR RED}I can't seem to find {COLOR GREEN}The Great Pokémon War{COLOR RED} book...\pI think someone checked it out before me..."), MSGBOX_AUTOCLOSE)
    applymovement(6, Common_Movement_WalkInPlaceUp)
    waitmovement(0)
    release
    end
}
script AlnerwickLibrary_Object_FatMan {
    lock
    faceplayer
    msgbox(format("{COLOR BLUE}To be quite honest, I've never checked out a book from here.\pTheir return policies are really bad, so I usually read the title and consider that to be reading."), MSGBOX_AUTOCLOSE)
    applymovement(3, Common_Movement_WalkInPlaceUp)
    waitmovement(0)
    release
    end
}
script AlnerwickLibrary_Object_Artist {
    lock
    faceplayer
    msgbox(format("{COLOR RED}I would personally consider books to be art as well.\pArt is just telling a story through different mediums."), MSGBOX_AUTOCLOSE)
    applymovement(4, Common_Movement_WalkInPlaceUp)
    waitmovement(0)
    release
    end
}

// trigger

const GRUNT_ID = 1
const WOMAN_ID = 2
const PLAYER_ID = OBJ_EVENT_ID_PLAYER

script AlnerwickLibrary_Mapscript_Librarian {
    setobjectxyperm(WOMAN_ID, 1, 3)
    setobjectmovementtype(WOMAN_ID, MOVEMENT_TYPE_FACE_UP)
    return
}

script AlnerwickLibrary_Trigger_Thief {
    if (var(VAR_ALNERWICKLIBRARY_THIEF) == 1) {
        lockall
        fadenewbgm(MUS_AQUA_MAGMA_HIDEOUT)
        special(SpawnCameraObject)
        getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
        if (var(VAR_TEMP_1) == 5) {
            applymovement(OBJ_EVENT_ID_CAMERA, AlnerwickLibrary_Thief_CMovement_0)
            waitmovement(0)
        } elif (var(VAR_TEMP_1) == 6) {
            applymovement(OBJ_EVENT_ID_CAMERA, AlnerwickLibrary_Thief_CMovement_1)
            waitmovement(0)
        } elif (var(VAR_TEMP_1) == 7) {
            applymovement(OBJ_EVENT_ID_CAMERA, AlnerwickLibrary_Thief_CMovement_2)
            waitmovement(0)
        }
        delay(5)
        applymovement(GRUNT_ID, Common_Movement_WalkInPlaceDown)
        waitmovement(0)
        msgbox(format("???: {COLOR BLUE}Wha' do ya mean I can't take this ol' dusty book?"), MSGBOX_AUTOCLOSE)
        delay(10)
        applymovement(WOMAN_ID, Common_Movement_WalkInPlaceUp)
        waitmovement(0)
        msgbox(format("Librarian: {COLOR RED}I'm sorry sir, but the {COLOR GREEN}Akumutome{COLOR RED} cannot be checked out from the library."), MSGBOX_AUTOCLOSE)
        delay(10)
        applymovement(GRUNT_ID, AlnerwickLibrary_Thief_Movement_1)
        playse(SE_PIN)
        waitse
        waitmovement(0)
        msgbox(format("???: {COLOR BLUE}Checked out?{PAUSE 10}!\pBahaha!\pI'm not checkin' this out!"), MSGBOX_AUTOCLOSE)
        applymovement(GRUNT_ID, AlnerwickLibrary_Thief_Movement_0)
        applymovement(OBJ_EVENT_ID_CAMERA, AlnerwickLibrary_Thief_CMovement_3)
        waitmovement(0)
        if (var(VAR_TEMP_1) == 6) {
            msgbox(format("???: {COLOR BLUE}Move it kid!"), MSGBOX_AUTOCLOSE)
            applymovement(PLAYER_ID, AlnerwickLibrary_Thief_Movement_4)
            waitmovement(0)
            applymovement(GRUNT_ID, AlnerwickLibrary_Thief_Movement_0_1)
            waitmovement(0)
            removeobject(GRUNT_ID)
            playse(SE_EXIT)
        } else {
            applymovement(GRUNT_ID, AlnerwickLibrary_Thief_Movement_0_1)
            waitmovement(0)
            removeobject(GRUNT_ID)
            playse(SE_EXIT)
        }
        // return camera to player's position
        fadenewbgm(MUS_LILYCOVE_MUSEUM)
        if (var(VAR_TEMP_1) == 5) {
            applymovement(OBJ_EVENT_ID_CAMERA, AlnerwickLibrary_Thief_CMovement_4)
            waitmovement(0)
        } elif (var(VAR_TEMP_1) == 6) {
            applymovement(OBJ_EVENT_ID_CAMERA, AlnerwickLibrary_Thief_CMovement_5)
            waitmovement(0)
        } elif (var(VAR_TEMP_1) == 7) {
            applymovement(OBJ_EVENT_ID_CAMERA, AlnerwickLibrary_Thief_CMovement_5)
            waitmovement(0)
        }
        applymovement(WOMAN_ID, AlnerwickLibrary_Thief_Movement_2)
        playse(SE_PIN)
        waitse
        waitmovement(0)
        special(RemoveCameraObject)
        delay(15)
        // librarian walks towards player
        if (var(VAR_TEMP_1) == 5) {
            applymovement(WOMAN_ID, AlnerwickLibrary_Thief_Movement_3_0)
            waitmovement(0)
        } elif (var(VAR_TEMP_1) == 6) {
            applymovement(WOMAN_ID, AlnerwickLibrary_Thief_Movement_3_2)
            waitmovement(0)
        } elif (var(VAR_TEMP_1) == 7) {
            applymovement(WOMAN_ID, AlnerwickLibrary_Thief_Movement_3_2)
            waitmovement(0)
        }
        applymovement(PLAYER_ID, Common_Movement_FaceLeft)
        waitmovement(0)
        delay(10)
        msgbox(format("Librarian: {COLOR RED}I'm sorry about that ruckus. That man just stole one of the oldest books in this library!\pThat book is an ancient tome that describes the Legendary Pokémon {COLOR GREEN}Darkrai{COLOR RED}.\p{COLOR DARK_GREY}Librarian: {COLOR GREEN}Darkrai{COLOR RED} is an ancient Pokémon who haunts people's dreams, but the tome lacks a full translation.\p{COLOR DARK_GREY}Librarian:{COLOR RED} I'm too old nowadays to be chasing down hooligans.\pDon't worry, he'll be fined when I get that book back."), MSGBOX_AUTOCLOSE)
        if (var(VAR_TEMP_1) == 5) {
            applymovement(WOMAN_ID, AlnerwickLibrary_Thief_Movement_5_0)
            waitmovement(0)
        } elif (var(VAR_TEMP_1) == 6) {
            applymovement(WOMAN_ID, AlnerwickLibrary_Thief_Movement_5_2)
            waitmovement(0)
        } elif (var(VAR_TEMP_1) == 7) {
            applymovement(WOMAN_ID, AlnerwickLibrary_Thief_Movement_5_2)
            waitmovement(0)
        }
        setflag(FLAG_ALNERWICKLIBRARY_THIEF)
        setvar(VAR_ALNERWICKLIBRARY_THIEF, 2)
        release
        end
    }
}
movement AlnerwickLibrary_Thief_Movement_0 {
    walk_fast_right
    walk_fast_down * 2
    walk_fast_right
}
movement AlnerwickLibrary_Thief_Movement_0_1 {
    walk_fast_right * 4
    walk_fast_up * 3
    walk_in_place_left
}
movement AlnerwickLibrary_Thief_Movement_1 {
    emote_exclamation_mark
    delay_16
    walk_in_place_down
}
movement AlnerwickLibrary_Thief_Movement_2 {
    walk_in_place_right
    emote_exclamation_mark
}
movement AlnerwickLibrary_Thief_Movement_3_0 {
    walk_right * 2
}
movement AlnerwickLibrary_Thief_Movement_3_1 {
    walk_down
    walk_right * 2
}
movement AlnerwickLibrary_Thief_Movement_3_2 {
    walk_down * 2
    walk_right * 2
}
movement AlnerwickLibrary_Thief_Movement_4 {
    walk_fast_down
    walk_in_place_fast_up
}
movement AlnerwickLibrary_Thief_Movement_5_0 {
    walk_left * 2
    walk_up * 2
}
movement AlnerwickLibrary_Thief_Movement_5_1 {
    walk_left * 2
    walk_up * 3
}
movement AlnerwickLibrary_Thief_Movement_5_2 {
    walk_left * 2
    walk_up * 4
}
movement AlnerwickLibrary_Thief_CMovement_0 {
    walk_left * 3
    walk_up
}
movement AlnerwickLibrary_Thief_CMovement_1 {
    walk_left * 3
    walk_up * 2
}
movement AlnerwickLibrary_Thief_CMovement_2 {
    walk_left * 3
    walk_up * 3
}
movement AlnerwickLibrary_Thief_CMovement_3 {
    walk_fast_right
    walk_fast_down * 2
    walk_fast_right * 2
}
movement AlnerwickLibrary_Thief_CMovement_4 {
    walk_up
}
movement AlnerwickLibrary_Thief_CMovement_5 {
    walk_down
}

script AlnerwickLibrary_Object_Librarian {
    msgbox(format("Librarian: {COLOR RED}I'm not too worried about that thug taking that old book.\pThe book is written in runes, which have yet to be translated.\pUnless he can translate, that book is worthless."), MSGBOX_NPC)
    end
}