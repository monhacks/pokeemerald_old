const SKIP = 12
const BARTENDER = 7
const PLAYER = OBJ_EVENT_ID_PLAYER
const CAMERA = OBJ_EVENT_ID_CAMERA

raw `
MystCity_CrawdauntPub_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, CrawdauntPub_OnTransition
	map_script MAP_SCRIPT_ON_FRAME_TABLE, CrawdauntPub_OnFrameTable
	map_script MAP_SCRIPT_ON_LOAD, CrawdauntPub_OnLoad
	.byte 0

CrawdauntPub_OnTransition:
	compare VAR_CYPRESS_PROTO_EVENT_STATE, 6
	call_if_eq CrawdauntPub_Mapscript_LoadPositions6
	compare VAR_CYPRESS_PROTO_EVENT_STATE, 7
	call_if_eq CrawdauntPub_Mapscript_LoadPositions7
	end

CrawdauntPub_OnFrameTable::
	map_script_2 VAR_CYPRESS_PROTO_EVENT_STATE, 6, CrawdauntPub_Mapscript_Investigation
	.2byte 0

CrawdauntPub_OnLoad:
	call_if_set FLAG_CRAWDAUNTPUB_BARTENDER, CrawdauntPub_Mapscript_OnLoad_OpenBarDoor
	end
`
script CrawdauntPub_Mapscript_OnLoad_OpenBarDoor {
	setmetatile(11, 11, 539, 0)
	setmetatile(11, 10, 575, 0)
	setmetatile(11, 9, 567, 0)
	return
}

script CrawdauntPub_Mapscript_LoadPositions6 {
	setobjectxyperm(SKIP, 6, 9)
	setobjectmovementtype(SKIP, MOVEMENT_TYPE_FACE_RIGHT)
	setobjectmovementtype(1, MOVEMENT_TYPE_FACE_LEFT)
	return
}
script CrawdauntPub_Mapscript_LoadPositions7 {
	setobjectxyperm(SKIP, 5, 8)
	setobjectmovementtype(SKIP, MOVEMENT_TYPE_LOOK_AROUND)
	return
}

script CrawdauntPub_Mapscript_Investigation {
	lockall
	applymovement(SKIP, Common_Movement_WalkInPlaceRight)
	waitmovement(0)
	msgbox(format("Skip: {COLOR BLUE}Answer my question!\pHave you seen any suspicious people in this pub?"))
	applymovement(1, Common_Movement_WalkInPlaceLeft)
	waitmovement(0)
	msgbox(format("Sailor: {COLOR BLUE}For the last time kid, I ain't tellin' you anything!\pIt's better to keep that information to yourself.\pYou don't know who you're getting involved with!"))
	applymovement(SKIP, Common_Movement_WalkInPlaceRight)
	waitmovement(0)
	msgbox(format("Skip: {COLOR BLUE}Are you implying that you know who they are?"))
	applymovement(1, Common_Movement_WalkInPlaceLeft)
	waitmovement(0)
	msgbox(format("Sailor: {COLOR BLUE}No kid!\nBecause I stay out of that business!\pNow scram before I make you!"), MSGBOX_AUTOCLOSE)
	applymovement(1, Common_Movement_WalkInPlaceRight)
	waitmovement(0)
	delay(10)
	applymovement(SKIP, Common_Movement_WalkInPlaceDown)
	waitmovement(0)
	msgbox(format("Skip: {COLOR BLUE}There you are slowpoke!"), MSGBOX_AUTOCLOSE)
	applymovement(SKIP, CrawdauntPub_Investigation_Movement_0)
	waitmovement(0)
	msgbox(format("Skip: {COLOR BLUE}None of these guys will talk!\pThey're all hard-heads who are afraid of those {COLOR GREEN}Team Proto{COLOR BLUE} goons.\pWe need to try to squeeze the information out of someone in here!\pYou do some talking for a minute, I need to cool off."), MSGBOX_AUTOCLOSE)
	delay(10)
	applymovement(SKIP, CrawdauntPub_Investigation_Movement_1)
	waitmovement(0)
	setobjectmovementtype(SKIP, MOVEMENT_TYPE_LOOK_AROUND)
	setvar(VAR_CYPRESS_PROTO_EVENT_STATE, 7)
	releaseall
	end
}
movement CrawdauntPub_Investigation_Movement_0 {
	walk_left
	walk_down * 2
}
movement CrawdauntPub_Investigation_Movement_1 {
	walk_up * 3
}

script CrawdauntPub_Object_Sailor_1 {
	msgbox(format("{COLOR BLUE}What's a kid like you doing in {COLOR GREEN}The Crawdaunt Pub{COLOR BLUE}?\pLookin' for trouble?\pScram kid!"), MSGBOX_NPC)
	end
}
script CrawdauntPub_Object_Sailor_2 {
	if (flag(FLAG_CRAWDAUNTPUB_SODA_POP_DUDE) == TRUE) {
		msgbox(format("{COLOR BLUE}How those Soda's tastin'?"), MSGBOX_NPC)
		release
		end
	}
	lock
	faceplayer
	msgbox(format("{COLOR BLUE}Hey kid!\nWant a beer?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == 1) {
		msgbox(format("{COLOR BLUE}Bahahaha!\pYou think you're slick kid?\pI ain't giving you any beer!\pNow get lost!"), MSGBOX_AUTOCLOSE)
		release
		end
	} else {
		msgbox(format("{COLOR BLUE}Good answer!\pHere, take these.\nI don't want 'em."), MSGBOX_AUTOCLOSE)
		giveitem(ITEM_SODA_POP, 5)
		setflag(FLAG_CRAWDAUNTPUB_SODA_POP_DUDE)
		release
		end
	}
}
script CrawdauntPub_Object_Sailor_3 {
	if (var(VAR_CYPRESS_PROTO_EVENT_STATE) == 7) {
		msgbox(format("{COLOR BLUE}Whadda want?\p............\n............\pBad guys eh?\pNope.\pI keep my mouth shut around here kid..."), MSGBOX_NPC)
		end
	} else {
		msgbox(format("{COLOR BLUE}Urgh...\pComin' here after working at {COLOR GREEN}Bayport Harbour{COLOR BLUE} is always a relief..."), MSGBOX_NPC)
		end
	}
}
script CrawdauntPub_Object_Man_1 {
	msgbox(format("{COLOR BLUE}Are you a trainer?\pMy son just recently left home to take on the {COLOR GREEN}Ryoko Pok√©mon League{COLOR BLUE}.\pI wonder how he's doing..."), MSGBOX_NPC)
	end
}
script CrawdauntPub_Object_Man_2 {
	if (var(VAR_CYPRESS_PROTO_EVENT_STATE) == 7) {
		msgbox(format("{COLOR BLUE}What kid?\p............\n............\pKid, I know you're young, but you shouldn't be messing with those people."), MSGBOX_NPC)
		end
	} else {
		msgbox(format("{COLOR BLUE}Will you grab me another beer?"), MSGBOX_NPC)
		end
	}
}
script CrawdauntPub_Object_FatMan {
	msgbox(format("Zzzzzzzz...\pThe Fat Man is sleeping."), MSGBOX_NPC)
	end
}
script CrawdauntPub_Object_Bartender {
	if (var(VAR_CYPRESS_PROTO_EVENT_STATE) == 7) {
		lockall
		faceplayer
		msgbox(format("Bartender: {COLOR BLUE}Sorry kid, we don't serve min--\pHuh?\pUhh... {COLOR GREEN}Team Proto{COLOR BLUE} you say?"))
		applymovement(BARTENDER, CrawdauntPub_Bartender_Movement_0)
		waitmovement(0)
		msgbox(format("Bartender: {COLOR BLUE}Who are you, kid?"), MSGBOX_AUTOCLOSE)
		applymovement(SKIP, Common_Movement_WalkInPlaceRight)
		waitmovement(0)
		applymovement(SKIP, Common_Movement_ExclamationMark)
		waitmovement(0)
		playse(SE_PIN)
		waitse
		msgbox(format("Skip: {COLOR BLUE}I knew someone would budge!"), MSGBOX_AUTOCLOSE)
		applymovement(SKIP, CrawdauntPub_Bartender_Movement_1)
		waitmovement(0)
		msgbox(format("Skip: {COLOR BLUE}What do you know about {COLOR GREEN}Team Proto{COLOR BLUE}?\pSpit it out!"), MSGBOX_AUTOCLOSE)
		applymovement(BARTENDER, Common_Movement_WalkInPlaceLeft)
		waitmovement(0)
		msgbox(format("Bartender: {COLOR BLUE}I-I... uhh..."), MSGBOX_AUTOCLOSE)
		applymovement(BARTENDER, CrawdauntPub_Bartender_Movement_2)
		waitmovement(0)
		playse(SE_KAIDAN)
		waitse
		applymovement(SKIP, Common_Movement_ExclamationMark)
		waitmovement(0)
		playse(SE_PIN)
		waitse
		msgbox(format("Skip: {COLOR BLUE}He's getting away!\pI'm going after him!"), MSGBOX_AUTOCLOSE)
		delay(10)
		applymovement(SKIP, CrawdauntPub_Bartender_Movement_3)
		waitmovement(0)
		setmetatile(11, 11, 539, 0)
		setmetatile(11, 10, 575, 0)
		setmetatile(11, 9, 567, 0)
		special(DrawWholeMapView)
		playse(SE_DOOR)
		waitse
		applymovement(SKIP, CrawdauntPub_Bartender_Movement_4)
		waitmovement(0)
		playse(SE_KAIDAN)
		waitse
		setflag(FLAG_CRAWDAUNTPUB_BARTENDER)
		setvar(VAR_CYPRESS_PROTO_EVENT_STATE, 8)
		removeobject(SKIP)
		removeobject(BARTENDER)
		releaseall
		end
	} else {
		msgbox(format("{COLOR BLUE}Sorry kid, we don't serve minors here.\pMove along, I got customers to serve."), MSGBOX_NPC)
		end
	}
}
movement CrawdauntPub_Bartender_Movement_0 {
	face_up
	delay_8
	face_down
	delay_8
	face_right
	delay_8
	face_left
}
movement CrawdauntPub_Bartender_Movement_1 {
	walk_right
}
movement CrawdauntPub_Bartender_Movement_2 {
	walk_fast_right
	walk_fast_up * 3
	set_invisible
}
movement CrawdauntPub_Bartender_Movement_3 {
	walk_fast_down * 3
	walk_fast_right * 5
	face_up
}
movement CrawdauntPub_Bartender_Movement_4 {
	walk_fast_up * 3
	walk_fast_left
	walk_fast_up * 3
	set_invisible
}

script CrawdauntPub_Object_Cook_1 {
	msgbox(format("{COLOR BLUE}Hey hey!\nWatch it kid!\pMake sure to shout when you're coming through the kitchen!"), MSGBOX_NPC)
	end
}
script CrawdauntPub_Object_Cook_2 {
	msgbox(format("{COLOR BLUE}Sleeves up when you're in the kitchen!\pDon't want yourself getting burned, do ya?"), MSGBOX_NPC)
	end
}
script CrawdauntPub_Object_Cook_3 {
	msgbox(format("{COLOR BLUE}Hm?\nThe staircase in the corner?\p............\pI don't know what you're talking about...\pAnyways, back to work!"), MSGBOX_NPC)
	end
}
script CrawdauntPub_Object_Cook_4 {
	msgbox(format("{COLOR BLUE}Hmmm...\pI can't find the lettuce..."), MSGBOX_NPC)
	end
}