raw `
Davnridge_Mines_MapScripts::
	.byte 0

`

script DavnridgeMines_Sign_LocationName {
    msgbox(format("{COLOR LIGHT_GREEN}Davnridge Mines\p{COLOR DARK_GREY}There is spray paint over the rest of the sign..."), MSGBOX_SIGN, MSGBOX_SUB_WOOD)
    end
}

const GUARD = 5
const PLAYER = OBJ_EVENT_ID_PLAYER
const CAMERA = OBJ_EVENT_ID_CAMERA

script DavnridgeMines_Trigger_NoPassage {
    lock
    applymovement(GUARD, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    applymovement(GUARD, Common_Movement_ExclamationMark)
    waitmovement(0)
    delay(15)
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    if (var(VAR_TEMP_1) == 29) {
        applymovement(GUARD, DavnridgeMines_Movement_NoPassage_0)
        waitmovement(0)
    }
    applymovement(PLAYER, Common_Movement_WalkInPlaceUp)
    waitmovement(0)
    msgbox(format("{COLOR BLUE}Hey bud!\nThis are over here is off limits right now.\pWe're running some explosion tests, so no one is allowed through.\pCome back later when we're done."), MSGBOX_AUTOCLOSE)
    delay(5)
    applymovement(PLAYER, DavnridgeMines_Movement_NoPassage_1)
    waitmovement(0)
    if (var(VAR_TEMP_0) == 29) {
        applymovement(GUARD, DavnridgeMines_Movement_NoPassage_2)
        waitmovement(0)
    }
    release
    end
}
movement DavnridgeMines_Movement_NoPassage_0 {
    walk_down
}
movement DavnridgeMines_Movement_NoPassage_1 {
    walk_left
}
movement DavnridgeMines_Movement_NoPassage_2 {
    walk_up
    delay_4
    walk_in_place_down
}

//
const LILITH = 9

script DavnridgeMines_Trigger_Lilith {
    lock
    special(SpawnCameraObject)
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    if (var(VAR_TEMP_0) == 27) {
        applymovement(CAMERA, DavnridgeMines_Lilith_CMovement_1)
        waitmovement(0)
    } elif (var(VAR_TEMP_0) == 28) {
        applymovement(CAMERA, DavnridgeMines_Lilith_CMovement_0)
        waitmovement(0)
    } elif (var(VAR_TEMP_0) == 29) {
        applymovement(CAMERA, DavnridgeMines_Lilith_CMovement_2)
        waitmovement(0)
    }
    delay(10)
    applymovement(LILITH, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    msgbox(format("???: {COLOR RED}Good job Sudowoodo!\nNow, we should head back to the Gym."), MSGBOX_AUTOCLOSE)
    delay(10)
    applymovement(LILITH, Common_Movement_WalkInPlaceUp)
    waitmovement(0)
    applymovement(LILITH, Common_Movement_ExclamationMark)
    waitmovement(0)
    delay(10)
    msgbox(format("???: {COLOR RED}What are you doing in these parts?"), MSGBOX_AUTOCLOSE)
    delay(5)
    if (var(VAR_TEMP_0) == 27) {
        applymovement(LILITH, DavnridgeMines_Lilith_Movement_1)
        applymovement(CAMERA, DavnridgeMines_Lilith_CMovement_4)
        waitmovement(0)
        applymovement(PLAYER, Common_Movement_WalkInPlaceRight)
        waitmovement(0)
    } elif (var(VAR_TEMP_0) == 28) {
        applymovement(LILITH, DavnridgeMines_Lilith_Movement_0)
        applymovement(CAMERA, DavnridgeMines_Lilith_CMovement_3)
        waitmovement(0)
    } elif (var(VAR_TEMP_0) == 29) {
        applymovement(LILITH, DavnridgeMines_Lilith_Movement_2)
        applymovement(CAMERA, DavnridgeMines_Lilith_CMovement_5)
        waitmovement(0)
        applymovement(PLAYER, Common_Movement_WalkInPlaceLeft)
        waitmovement(0)
    }
    special(RemoveCameraObject)
    delay(10)
    msgbox(format("???: {COLOR RED}Wait, you don't need to tell me!\pI can tell you're a trainer who wants to challenge me!\p{COLOR DARK_GREY}Lilith: {COLOR RED}Hi, my name is Lilith.\nI'm {COLOR GREEN}Davnridge's Gym Leader{COLOR RED}!\pCome meet me at my Gym and I'll give you a challenge!"), MSGBOX_AUTOCLOSE)
    if (var(VAR_TEMP_0) == 28) {
        applymovement(LILITH, DavnridgeMines_Lilith_Movement_3)
        waitmovement(0)
    } else {
        applymovement(LILITH, DavnridgeMines_Lilith_Movement_4)
        waitmovement(0)
    }
    removeobject(LILITH)
    playse(SE_KAIDAN)
    setflag(FLAG_DAVNRIDGEMINES_LILITH)
    setvar(VAR_DAVNRIDGEMINES_STATE, 1)
    release
    end
}
movement DavnridgeMines_Lilith_CMovement_0 {
    walk_down * 3
}
movement DavnridgeMines_Lilith_CMovement_1 {
    walk_right
    walk_down * 3
}
movement DavnridgeMines_Lilith_CMovement_2 {
    walk_left
    walk_down * 3
}
movement DavnridgeMines_Lilith_Movement_0 {
    walk_up * 2
}
movement DavnridgeMines_Lilith_Movement_1 {
    walk_up * 3
    walk_in_place_left
}
movement DavnridgeMines_Lilith_Movement_2 {
    walk_up * 3
    walk_in_place_right
}
movement DavnridgeMines_Lilith_CMovement_3 {
    walk_up * 3
}
movement DavnridgeMines_Lilith_CMovement_4 {
    walk_up * 3
    walk_left
}
movement DavnridgeMines_Lilith_CMovement_5 {
    walk_up * 3
    walk_right
}
movement DavnridgeMines_Lilith_Movement_3 {
    walk_right
    walk_up * 4
    walk_left
}
movement DavnridgeMines_Lilith_Movement_4 {
    walk_up * 3
}

// trainers
script DavnridgeMines_Trainer_Miner_1 {
    trainerbattle_single(TRAINER_MARCEL, format("{COLOR BLUE}Wha' brings you to th' mines?"), format("Wowza!\nYou got some stron' Pokémon ther'!"))
    msgbox(format("{COLOR BLUE}I bet you'd be lookin' for ol' {COLOR GREEN}Lilith{COLOR BLUE}...\pI saw her go up those stairs ther'."), MSGBOX_NPC)
    end
}
script DavnridgeMines_Trainer_Miner_2 {
    trainerbattle_single(TRAINER_ALBERTO, format("{COLOR BLUE}I'm on my 15 minute break.\pLet's make this battle quick!"), format("Looks like my break is over..."))
    msgbox(format("{COLOR BLUE}Looks like my 15 minute break is over..."), MSGBOX_NPC)
    end
}
script DavnridgeMines_Trainer_Hiker {
    trainerbattle_single(TRAINER_ED, format("{COLOR BLUE}That guy over there won't let me through the caves!"), format("Only if I was able to get through the reset of the caves..."))
    msgbox(format("{COLOR BLUE}The rest of the caves are on the east passage in this tunnel here.\pThat passage will take you outside the caves and towards {COLOR GREEN}Enclava Town{COLOR BLUE}."), MSGBOX_NPC)
    end
}

// npcs
script DavnridgeMines_Object_Miner_3 {
    msgbox(format("{COLOR BLUE}Oh you want to battle?\pI didn't bring my Pokémon with me today...\lMy daughter wanted to play with them today."), MSGBOX_NPC)
    end
}
script DavnridgeMines_Object_Miner_NoPassage {
    msgbox(format("{COLOR BLUE}Hey bud!\nThis are over here is off limits right now.\pWe're running some explosion tests, so no one is allowed through.\pCome back later when we're done."), MSGBOX_NPC)
    end
}
