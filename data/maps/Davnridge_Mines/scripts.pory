raw `
Davnridge_Mines_MapScripts::
	.byte 0

`

script DavnridgeMines_Sign_LocationName {
    msgbox(format("{COLOR LIGHT_GREEN}Davnridge Mines\p{COLOR DARK_GREY}There is spray paint over the rest of the sign..."), MSGBOX_SIGN, MSGBOX_SUB_WOOD)
    end
}

const GUARD = 1
const PLAYER = OBJ_EVENT_ID_PLAYER
const CAMERA = OBJ_EVENT_ID_CAMERA

script DavnridgeMines_Trigger_NoPassage {
    lock
    applymovement(GUARD, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    applymovement(GUARD, Common_Movement_ExclamationMark)
    waitmovement(0)
    playse(SE_PIN)
    waitse
    msgbox(format("{COLOR BLUE}Whoa whoa!"))
    applymovement(PLAYER, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox(format("{COLOR BLUE}This area is off limits!\pWe're running some explosive tests in the mine up ahead.\pCome back later."), MSGBOX_AUTOCLOSE)
    applymovement(PLAYER, DavnridgeMines_Movement_NoPassage_0)
    waitmovement(0)
    release
    end
}
movement DavnridgeMines_Movement_NoPassage_0 {
    walk_down
}
movement DavnridgeMines_Movement_NoPassage_2 {
    walk_up
    delay_4
    walk_in_place_down
}

//
const LILITH = 7

script DavnridgeMines_Trigger_Lilith {
    lockall
    special(SpawnCameraObject)
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    if (var(VAR_TEMP_1) == 55) {
        applymovement(CAMERA, DavnridgeMines_Lilith_CMovement_0)
        waitmovement(0)
    } elif (var(VAR_TEMP_1) == 56) {
        applymovement(CAMERA, DavnridgeMines_Lilith_CMovement_1)
        waitmovement(0)
    } elif (var(VAR_TEMP_1) == 57) {
        applymovement(CAMERA, DavnridgeMines_Lilith_CMovement_2)
        waitmovement(0)
    }
    delay(16)
    msgbox(format("???: {COLOR RED}Cranidos, use Rock Tomb!"), MSGBOX_AUTOCLOSE)
    playmoncry(SPECIES_CRANIDOS, 0)
    waitmoncry
    delay(5)
    setvar(VAR_0x8004, 0) // vertical pan
    setvar(VAR_0x8005, 4) // horizontal pan
    setvar(VAR_0x8006, 2) // num of shakes
    setvar(VAR_0x8007, 1) // shake delay
    special(ShakeCamera)
    waitstate
    delay(10)
    msgbox(format("???: {COLOR RED}Great job Cranidos!\nReturn!"), MSGBOX_AUTOCLOSE)
    delay(15)
    applymovement(LILITH, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    applymovement(LILITH, Common_Movement_QuestionMark)
    waitmovement(0)
    playse(SE_PIN)
    waitse
    msgbox(format("???: {COLOR RED}You don't look like one of my miners..."))
    applymovement(LILITH, Common_Movement_ExclamationMark)
    waitmovement(0)
    playse(SE_PIN)
    waitse
    msgbox(format("???: {COLOR RED}Oh!\pYou're a trainer!"))
    if (var(VAR_TEMP_1) == 55) {
        applymovement(LILITH, DavnridgeMines_Lilith_Movement_1)
        applymovement(CAMERA, DavnridgeMines_Lilith_CMovement_3)
        waitmovement(0)
        applymovement(PLAYER, Common_Movement_WalkInPlaceDown)
        waitmovement(0)
    } elif (var(VAR_TEMP_1) == 56) {
        applymovement(LILITH, DavnridgeMines_Lilith_Movement_2)
        applymovement(CAMERA, DavnridgeMines_Lilith_CMovement_4)
        waitmovement(0)
    } elif (var(VAR_TEMP_1) == 57) {
        applymovement(LILITH, DavnridgeMines_Lilith_Movement_3)
        applymovement(CAMERA, DavnridgeMines_Lilith_CMovement_5)
        waitmovement(0)
        applymovement(PLAYER, Common_Movement_WalkInPlaceUp)
        waitmovement(0)
    }
    delay(10)
    msgbox(format("???: {COLOR RED}Nice to meet you, trainer!\pMy name is {COLOR GREEN}Lilith{COLOR RED} and I'm the {COLOR GREEN}Davnridge Pokémon Gym Leader{COLOR RED}.\pI assume you came all this way to challenge me, so I'll be waiting at my gym!\p{COLOR DARK_GREY}Lilith:{COLOR RED} I look forward to challenging you, trainer!"), MSGBOX_AUTOCLOSE)
    if (var(VAR_TEMP_1) == 56) {
        applymovement(LILITH, DavnridgeMines_Lilith_Movement_4)
        waitmovement(0)
    } else {
        applymovement(LILITH, DavnridgeMines_Lilith_Movement_5)
        waitmovement(0)
    }
    removeobject(LILITH)
    special(RemoveCameraObject)
    setflag(FLAG_DAVNRIDGEMINES_LILITH)
    setvar(VAR_DAVNRIDGEMINES_STATE, 1)
    releaseall
    end
}
movement DavnridgeMines_Lilith_CMovement_0 {
    walk_down * 1
    walk_left * 3
}
movement DavnridgeMines_Lilith_CMovement_1 {
    walk_left * 3
}
movement DavnridgeMines_Lilith_CMovement_2 {
    walk_up * 1
    walk_left * 3
}
movement DavnridgeMines_Lilith_Movement_1 {
    walk_right * 3
    walk_in_place_up
}
movement DavnridgeMines_Lilith_Movement_2 {
    walk_right * 2
}
movement DavnridgeMines_Lilith_Movement_3 {
    walk_right * 3
    walk_in_place_down
}
movement DavnridgeMines_Lilith_CMovement_3 {
    walk_right * 3
    walk_up
}
movement DavnridgeMines_Lilith_CMovement_4 {
    walk_right * 3
}
movement DavnridgeMines_Lilith_CMovement_5 {
    walk_right * 3
    walk_down
}
movement DavnridgeMines_Lilith_Movement_4 {
    walk_up
    walk_right * 6
    walk_up * 6
}
movement DavnridgeMines_Lilith_Movement_5 {
    walk_right * 5
    walk_up * 5
}


// trainers
script DavnridgeMines_Trainer_Miner_1 {
    trainerbattle_single(TRAINER_MINER_CLARK, format("{COLOR BLUE}Wha' brings you to th' mines?"), format("Wowza!\nYou got some stron' Pokémon ther'!"))
    msgbox(format("{COLOR BLUE}I bet you'd be lookin' for ol' {COLOR GREEN}Lilith{COLOR BLUE}...\pI saw her go up those stairs ther'."), MSGBOX_NPC)
    end
}
script DavnridgeMines_Trainer_Miner_2 {
    trainerbattle_single(TRAINER_MINER_LUIS, format("{COLOR BLUE}I'm on my 15 minute break.\pLet's make this battle quick!"), format("Looks like my break is over..."))
    msgbox(format("{COLOR BLUE}Looks like my 15 minute break is over..."), MSGBOX_NPC)
    end
}
script DavnridgeMines_Trainer_Hiker {
    trainerbattle_single(TRAINER_HIKER_ED, format("{COLOR BLUE}That guy over there won't let me through the caves!"), format("Only if I was able to get through the reset of the caves..."))
    msgbox(format("{COLOR BLUE}The rest of the caves are on the east passage in this tunnel here.\pThat passage will take you outside the caves and towards {COLOR GREEN}Enclava Town{COLOR BLUE}."), MSGBOX_NPC)
    end
}
script DavnridgeMines_Trainer_Fisherman {
    trainerbattle_single(TRAINER_FISHERMAN_JAMARCUS, format("{COLOR BLUE}This is a great fishing spot!"), format("Agh!\nYou scared the fish away!"))
    msgbox(format("{COLOR BLUE}This is my secret fishing spot!\pMy buddies always ask me how I get such big Magikarp!"), MSGBOX_NPC)
    end
}
script DavnridgeMines_Trainer_Maniac {
    trainerbattle_single(TRAINER_MANIAC_IAN, format("{COLOR BLUE}I found a rare Pokémon!\nWant to see it?"), format("No!\nMy rare Pokémon!"))
    msgbox(format("{COLOR BLUE}There is a very rare chance to find a Gligar in these mines."), MSGBOX_NPC)
    end
}
script DavnridgeMines_Trainer_BlackBelt {
    trainerbattle_single(TRAINER_BLACKBELT_ALVARO, format("{COLOR BLUE}Hai-ya!"), format("Fwah!"))
    msgbox(format("{COLOR BLUE}Hwah!"), MSGBOX_NPC)
    end
}

// npcs
script DavnridgeMines_Object_Miner_3 {
    msgbox(format("{COLOR BLUE}Oh you want to battle?\pI didn't bring my Pokémon with me today...\lMy daughter wanted to play with them today."), MSGBOX_NPC)
    end
}
script DavnridgeMines_Object_Miner_NoPassage {
    msgbox(format("{COLOR BLUE}Hey bud!\nThis tunnel over here is off limits right now.\pWe're running some explosion tests, so no one is allowed through.\pCome back later when we're done."), MSGBOX_NPC)
    end
}
