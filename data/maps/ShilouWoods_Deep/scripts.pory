mapscripts ShilouWoods_Deep_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		setflashradius(2)
	}
}

const BOY = 1
const PLAYER = OBJ_EVENT_ID_PLAYER

script ShilouWoodsDeep_Trigger_LittleBoy_Event {
	lock
	fadeoutbgm(3)
	delay(130)
	playse(SE_M_PERISH_SONG)
	waitse
	applymovement(BOY, ShilouWoodsDeep_LittleBoyEvent_Movement_0)
	waitmovement(0)
	getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
	if (var(VAR_TEMP_0) == 7) { // left
		applymovement(BOY, ShilouWoodsDeep_LittleBoyEvent_Movement_2)
		waitmovement(0)
		applymovement(PLAYER, Common_Movement_WalkInPlaceLeft)
		waitmovement(0)
	} elif (var(VAR_TEMP_0) == 6) { // right
		applymovement(BOY, ShilouWoodsDeep_LittleBoyEvent_Movement_1)
		waitmovement(0)
		applymovement(PLAYER, Common_Movement_WalkInPlaceRight)
		waitmovement(0)
	}
	msgbox(format("{COLOR BLUE}{PAUSE 10}.{PAUSE 10}.{PAUSE 10}h{PAUSE 10}i{PAUSE 10}.{PAUSE 10}.{PAUSE 10}.{PAUSE 10}.{PAUSE 10}.{PAUSE 10}.{PAUSE 10}.{PAUSE 10}.\n{PAUSE 10}S{PAUSE 10}T{PAUSE 10}R{PAUSE 10}A{PAUSE 10}N{PAUSE 10}G{PAUSE 10}E{PAUSE 10}R{PAUSE 10}.{PAUSE 10}.{PAUSE 10}."))
	giveitem(ITEM_SPELL_TAG, 1)
	delay(20)
	fadescreen(FADE_TO_BLACK)
	delay(15)
	playse(SE_M_NIGHTMARE)
	waitse
	delay(15)
	removeobject(BOY)
	setflag(FLAG_SHILOUWOODS_GHOST_BOY)
	fadescreen(FADE_FROM_BLACK)
	setvar(VAR_SHILOUWOODS_GHOST_BOY, 2)
	delay(20)
	fadeinbgm(MUS_RG_POKE_TOWER)
	release
	end
}
script ShilouWoodsDeep_Trigger_HaventTalkedToLittleGirl {
	lock
	playse(SE_M_NIGHTMARE)
	waitse
	msgbox(format("A mysterious force prevents you from going further..."), MSGBOX_AUTOCLOSE)
	applymovement(PLAYER, Common_Movement_WalkLeft)
	waitmovement(0)
	release
	end
}
movement ShilouWoodsDeep_LittleBoyEvent_Movement_0 {
	face_down
}
movement ShilouWoodsDeep_LittleBoyEvent_Movement_1 {
	walk_slow_down
	walk_slow_left
	walk_slow_down * 2
	walk_in_place_slow_left
}
movement ShilouWoodsDeep_LittleBoyEvent_Movement_2 {
	walk_slow_down
	walk_slow_left * 2
	walk_slow_down * 2
	walk_in_place_slow_right
}