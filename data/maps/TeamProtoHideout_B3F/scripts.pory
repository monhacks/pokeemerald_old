mapscripts TeamProtoHideout_B3F_MapScripts {}

// trainers

script ProtoHideoutB3F_Trainer_Grunt4 {
	trainerbattle_single(TRAINER_GRUNT_HIDEOUT_4, format("{COLOR BLUE}A-ha!\pMy storage room patrolling was worth it!"), format("Aw..."))
	msgbox(format("{COLOR BLUE}I like it in here, I don't like to interact with anyone."), MSGBOX_NPC)
	end
}
script ProtoHideoutB3F_Trainer_Scientist7 {
	trainerbattle_single(TRAINER_SCIENTIST_HIDEOUT_7, format("{COLOR BLUE}I've been trying to fix these wires here for a while, but a little battle wouldn't hurt."), format("...\nNow back to work I guess..."))
	msgbox(format("{COLOR BLUE}I can't figure out where these wires are suppose to connect..."), MSGBOX_NPC)
	end
}

// objects

const SKIP = 8
const APOLLO = 3
const HAUMEA = 6 // pink hair
const ERIS = 5 // silver hair
const CERES = 4 // red hair
const PLAYER = OBJ_EVENT_ID_PLAYER
const CAMERA = OBJ_EVENT_ID_CAMERA

script ProtoHideoutB3F_Object_Skip {
	lockall
	faceplayer
	msgbox(format("Skip: {COLOR BLUE}Took you long enough to find me!\pThis gate is blocking a meeting room it seems.\pI've already had to do everything for this operation, so go make yourself useful and go find a key!\pI'm sure some guard knows where it is."))
	checkitem(ITEM_BASEMENT_KEY, 1)
	if (var(VAR_RESULT) == TRUE) {
		applymovement(SKIP, Common_Movement_QuestionMark)
		waitmovement(0)
		playse(SE_PIN)
		waitse
		msgbox(format("Skip: {COLOR BLUE}Wait, you found a key?\pHuh!\nI'm impressed.\p{COLOR DARK_GREY}Skip:{COLOR BLUE} Let's see if this will work..."), MSGBOX_AUTOCLOSE)
		getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
		if (var(VAR_TEMP_0) == 1 || var(VAR_TEMP_0) == 3) {
			applymovement(PLAYER, ProtoHideoutB3F_KeyUnlock_Movement_2)
			applymovement(SKIP, ProtoHideoutB3F_KeyUnlock_Movement_0)
			waitmovement(0)
		} elif (var(VAR_TEMP_0) == 2) {
			if (var(VAR_TEMP_1) == 10) {
				applymovement(PLAYER, ProtoHideoutB3F_KeyUnlock_Movement_2)
				applymovement(SKIP, ProtoHideoutB3F_KeyUnlock_Movement_1)
				waitmovement(0)
			} elif (var(VAR_TEMP_1) == 8) {
				applymovement(SKIP, ProtoHideoutB3F_KeyUnlock_Movement_0)
				waitmovement(0)
			}
		}
		delay(20)
		setmetatile(1, 11, 513, 0)
		setmetatile(1, 12, 513, 0)
		setmetatile(2, 11, 512, 0)
		setmetatile(2, 12, 512, 0)
		setmetatile(3, 11, 512, 0)
		setmetatile(3, 12, 512, 0)
		special(DrawWholeMapView)
		playse(SE_BREAKABLE_DOOR)
		delay(25)
		applymovement(SKIP, Common_Movement_WalkInPlaceUp)
		waitmovement(0)
		msgbox(format("Skip: {COLOR BLUE}It opened!\pNow it's time to battle these goons!\pLet's go, you and me!"), MSGBOX_AUTOCLOSE)
		getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
		if (var(VAR_TEMP_0) == 1) {
			applymovement(PLAYER, ProtoHideoutB3F_KeyUnlock_Movement_3)
			waitmovement(0)
		} elif (var(VAR_TEMP_0) == 3) {
			applymovement(PLAYER, ProtoHideoutB3F_KeyUnlock_Movement_4)
			waitmovement(0)
		} elif (var(VAR_TEMP_0) == 2) {
			if (var(VAR_TEMP_1) == 8) {
				applymovement(PLAYER, ProtoHideoutB3F_KeyUnlock_Movement_5)
				waitmovement(0)
			}
		}
		delay(10)
		applymovement(SKIP, ProtoHideoutB3F_KeyUnlock_Movement_6)
		applymovement(PLAYER, ProtoHideoutB3F_KeyUnlock_Movement_6)
		waitmovement(0)
		special(SpawnCameraObject)
		delay(10)
		applymovement(CAMERA, ProtoHideoutB3F_KeyUnlock_CMovement_0)
		waitmovement(0)
		// start of the apollo admins dialogue
		applymovement(APOLLO, Common_Movement_WalkInPlaceDown)
		waitmovement(0)
		msgbox(format("???: {COLOR GREEN}Haumea{COLOR BLUE}, did you obtain the {COLOR GREEN}Akumutome{COLOR BLUE} from {COLOR GREEN}Alnerwick Library{COLOR BLUE}?"))
		applymovement(HAUMEA, Common_Movement_WalkInPlaceLeft)
		waitmovement(0)
		msgbox(format("Haumea: {COLOR BLUE}Yes sir.\pOne of our Grunts stole it from the library."))
		applymovement(APOLLO, Common_Movement_WalkInPlaceDown)
		waitmovement(0)
		msgbox(format("???: {COLOR BLUE}Good.\p{COLOR GREEN}Eris{COLOR BLUE}, what's the status on the {COLOR GREEN}Dorimtome{COLOR BLUE}?"))
		applymovement(ERIS, Common_Movement_WalkInPlaceUp)
		waitmovement(0)
		msgbox(format("Eris: {COLOR RED}We had to get in a bit of trouble with {COLOR GREEN}Professor Cypress{COLOR RED} and another trainer, but they shouldn't be a problem anymore."))
		applymovement(APOLLO, Common_Movement_WalkInPlaceDown)
		waitmovement(0)
		msgbox(format("???: {COLOR BLUE}Hmph. We can't have any more interruptions.\pOnce both the {COLOR GREEN}Akumutome{COLOR BLUE} and {COLOR GREEN}Dorimtome{COLOR BLUE} are fully translated, we can track down and summon the legendary {COLOR GREEN}Darkrai{COLOR BLUE}.\p{COLOR DARK_GREY}???: {COLOR BLUE}Then finally, {COLOR GREEN}Ryoko{COLOR BLUE} will respect my wishes and fall into my ruling!"))
		applymovement(CERES, Common_Movement_WalkInPlaceRight)
		waitmovement(0)
		msgbox(format("Ceres: {COLOR RED}Sir, I don't mean to interrupt, but there have been reports of two trainers discovering the hideout.\pWe need to leave immediately."))
		applymovement(APOLLO, Common_Movement_WalkInPlaceDown)
		waitmovement(0)
		msgbox(format("???: {COLOR BLUE}Another interruption...\pI'd like to confront these trainers."), MSGBOX_AUTOCLOSE)
		delay(10)
		applymovement(SKIP, Common_Movement_JumpInPlaceRight)
		playse(SE_BIKE_HOP)
		waitse
		waitmovement(0)
		applymovement(SKIP, Common_Movement_WalkInPlaceRight)
		waitmovement(0)
		msgbox(format("Skip: {COLOR BLUE}We're here to talk to you!"))
		closemessage
		delay(5)
		applymovement(APOLLO, Common_Movement_WalkInPlaceLeft)
		applymovement(CERES, Common_Movement_WalkInPlaceLeft)
		applymovement(ERIS, Common_Movement_WalkInPlaceLeft)
		applymovement(HAUMEA, Common_Movement_WalkInPlaceLeft)
		waitmovement(0)
		applymovement(APOLLO, Common_Movement_ExclamationMark)
		applymovement(CERES, Common_Movement_ExclamationMark)
		applymovement(ERIS, Common_Movement_ExclamationMark)
		applymovement(HAUMEA, Common_Movement_ExclamationMark)
		playse(SE_PIN)
		waitse
		waitmovement(0)
		delay(15)
		applymovement(CAMERA, ProtoHideoutB3F_AdminEvent_CMovement_0)
		waitmovement(0)
		applymovement(CERES, ProtoHideoutB3F_AdminEvent_Movement_1)
		applymovement(APOLLO, ProtoHideoutB3F_AdminEvent_Movement_0)
		waitmovement(0)
		special(RemoveCameraObject)
		msgbox(format("???: {COLOR BLUE}You two.\pYou're the ones who are causing us trouble?\p{COLOR DARK_GREY}???: {COLOR BLUE}Pathetic.\pTwo amatuer Pok√©mon trainers blocking {COLOR GREEN}Team Proto{COLOR BLUE}'s path to glory."))
		applymovement(SKIP, Common_Movement_WalkInPlaceRight)
		waitmovement(0)
		msgbox(format("Skip: {COLOR BLUE}Your cloak wearing grunts are weak compared to me!"))
		applymovement(APOLLO, Common_Movement_WalkInPlaceLeft)
		waitmovement(0)
		msgbox(format("???: {COLOR BLUE}Is that so...\pI would like to see for myself."))
		msgbox(format("Apollo: {COLOR BLUE}My name is {COLOR GREEN}Apollo{COLOR BLUE}, leader of {COLOR GREEN}Team Proto{COLOR BLUE}!\pShow me your strength, I want to see why you two are such a nuisance!"))
		if (var(VAR_STARTER_MON) == 0) { // player choose rowlet, rival has chimchar
			multi_fixed_2_vs_2(TRAINER_LEADER_APOLLO_1, format("It seems I underestimated you two..."), TRAINER_ADMIN_CERES_1, format("Quite the challenge indeed."), TRAINER_SKIP2_FIRE, TRAINER_BACK_PIC_RED)
		} elif (var(VAR_STARTER_MON) == 1) { // player choose chimchar, rival has froakie
			multi_fixed_2_vs_2(TRAINER_LEADER_APOLLO_1, format("It seems I underestimated you two..."), TRAINER_ADMIN_CERES_1, format("Quite the challenge indeed."), TRAINER_SKIP2_WATER, TRAINER_BACK_PIC_RED)
		} else { // player choose froakie, rival has rowlet
			multi_fixed_2_vs_2(TRAINER_LEADER_APOLLO_1, format("It seems I underestimated you two..."), TRAINER_ADMIN_CERES_1, format("Quite the challenge indeed."), TRAINER_SKIP2_GRASS, TRAINER_BACK_PIC_RED)
		}
		specialvar(VAR_RESULT, GetBattleOutcome)
		if (var(VAR_RESULT) == B_OUTCOME_LOST) {
			fadescreen(FADE_TO_BLACK)
			special(SetCB2WhiteOut)
			waitstate
			releaseall
			end
		}
		delay(10)
		msgbox(format("Apollo: {COLOR BLUE}I'm quite surprised by the amount of skill you two displayed in that battle.\pI may have been defeated, but this is not the last of {COLOR GREEN}Team Proto{COLOR BLUE}."), MSGBOX_AUTOCLOSE)
		delay(10)
		fadescreen(FADE_TO_BLACK)
		removeobject(APOLLO)
		removeobject(HAUMEA)
		removeobject(CERES)
		removeobject(ERIS)
		playse(SE_WARP_OUT)
		waitse
		fadescreen(FADE_FROM_BLACK)
		delay(10)
		applymovement(SKIP, Common_Movement_WalkInPlaceUp)
		applymovement(PLAYER, Common_Movement_WalkInPlaceDown)
		waitmovement(0)
		msgbox(format("Skip: {COLOR BLUE}That guy sure was a little terrifying.\pBut I beat them!\pDid you see that performance by me?\pHa!\nI pretty much carried.\pAnyways, I'm out of here.\pI'm going to head over to {COLOR GREEN}Altos City{COLOR BLUE} and get my next gym badge!\p{COLOR DARK_GREY}Skip: {COLOR BLUE}See you around slowpoke!"), MSGBOX_AUTOCLOSE)
		applymovement(SKIP, ProtoHideoutB3F_AdminEvent_Movement_2)
		waitmovement(0)
		removeobject(SKIP)
		setvar(VAR_PROTO_HIDEOUT, 2)
		setflag(FLAG_PROTO_HIDEOUT_ALL_OBJS)
		releaseall
		end
	} else {
		closemessage
		release
		end
	}
}
movement ProtoHideoutB3F_KeyUnlock_Movement_0 {
	walk_down * 2
}
movement ProtoHideoutB3F_KeyUnlock_Movement_1 {
	walk_left
	walk_down * 2
	walk_right
	walk_in_place_down
}
movement ProtoHideoutB3F_KeyUnlock_Movement_2 {
	delay_16
	delay_4
	walk_in_place_down
}
movement ProtoHideoutB3F_KeyUnlock_Movement_3 { // player is to the left of SKIP's original position
	walk_right
	walk_down
}
movement ProtoHideoutB3F_KeyUnlock_Movement_4 { // player is to the right of SKIP's original position
	walk_left
	walk_down
}
movement ProtoHideoutB3F_KeyUnlock_Movement_5 { // player is above SKIP's original position
	walk_down * 2
}
movement ProtoHideoutB3F_KeyUnlock_Movement_6 {
	walk_down * 7
	walk_right * 3
}
movement ProtoHideoutB3F_KeyUnlock_CMovement_0 {
	walk_right * 4
}
movement ProtoHideoutB3F_AdminEvent_Movement_0 {
	walk_down
	walk_left * 3
	walk_down
	walk_in_place_left
}
movement ProtoHideoutB3F_AdminEvent_Movement_1 {
	walk_down
	walk_left
}
movement ProtoHideoutB3F_AdminEvent_CMovement_0 {
	walk_left * 4
}
movement ProtoHideoutB3F_AdminEvent_Movement_2 {
	walk_left * 3
	walk_up * 8
}